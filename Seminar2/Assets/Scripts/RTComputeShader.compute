//Svaki kernel određuje koju funkciju treba kompajlirati pri pokretanju sjenčara, nama treba samo jedna funkcija
#pragma kernel CSMain


// Stvori novi RenderTexture varijablu koja će se vratiti kao rezultat ekrana
RWTexture2D<float4> Result;

//Većina varijabli se predaju sjenčaru iz RayTracingComputeShader.cs skripte pomoću set funkcija

//rezolucija ekrana
float Resolution;

//pozicija kamere iz scene
float3 cameraPos;

//intenzitet ambijentalnog osvjetljenja
float4 ambientLight;

static const float PI = 3.14159265f;

//pozicija i intenzitet točkastog svijetla u sceni, program podržava samo jedan izvor osvjetljenja
float3 sceneLightPos;
float4 sceneLightIntensity;

//matrice vezane uz kameru u sceni, program podržava samo jednu kameru. Ove matrice su bitne za izračun zrake kroz piksel
float4x4 _CameraToWorldMatrix;
float4x4 _CameraInverseProjectionMatrix;

//struktura koja opisuje materijal sfere, svi koeficijenti se mogu podešavati u sceni kroz skriptu RayTracingMaterialSettings.cs
struct RayTracingMaterial {
	float4 ambientColor; //ambijentalni koeficijent
	float4 diffuseColor; //difuzni koeficijent
	float4 specularColor; //spekularni koeficijent
	float specularFactor; //spekularni faktor
	float indeksLoma;
	float reflectionCoef; //koeficijent refleksije
	float refractionCoef; //koeficijent refrakcije
};

//struktura koja opisuje sferu
struct Sphere {
    float radius;
    float3 position;
	RayTracingMaterial material;
};

//struktura koja opisuje informacije vezane uz presjek zrake i sfere u sceni
struct HitInfo {
    bool didHit;
	float distance; //udaljenost točke presjeka, bitno za određivanje ispravne (prve) točke presjeka
	float3 hitPoint;
	float3 normal; //normala u tocki presjeka
	RayTracingMaterial material;
	Sphere sphere; //pogođena sfera, sluzi za izuzimanje te sfere pri provjeri kolizije
};

//buffer koji prima sfere iz scene i sprema ih za provjeru presjeka
RWStructuredBuffer<Sphere> spheres;

//struktura koja opisuje zraku
struct Ray {
    float3 origin; //hvatište zrake
    float3 direction; //smjer zrake
	float3 energy; //"energija" zrake koja se gubi pri svakoj refleksiji ili refrakciji ovisno o koeficijnetima
};

//funkcija koja izračunava zraku sa hvatištem u poziciji kamere i koja ide kroz piksel na ekranu
Ray CreateCameraRay(float2 uv) 
{
	Ray newRay;
    float3 origin = mul(_CameraToWorldMatrix, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;
    newRay.origin = origin;
    float3 direction = mul(_CameraInverseProjectionMatrix, float4(uv, 0.0f, 1.0f)).xyz;
    direction = mul(_CameraToWorldMatrix, float4(direction, 0.0f)).xyz;
    direction = normalize(direction);
	newRay.direction = direction;
	newRay.energy = float3(1.0, 1.0, 1.0);
    
    return newRay;
}

//postavke sjenčara
int pogodioPozadinu = 0;
int blackWhite;
int ambient;
int diffuse;
int specular;
int reflection;
int refraction;
int shadows;
int blinnPhong;
int blackBackground;

int bounceNumber;


//testiranje presjeka izmedu sfera u sceni i izračunate zrake
HitInfo RaySphereIntersection(Ray ray, Sphere sphere) {
	//stvaranje praznog HitInfo
    HitInfo hitInfo = (HitInfo)0;
    float3 sphereCentre = sphere.position;
    float sphereRadius = sphere.radius;
    float3 offsetRayOrigin = ray.origin - sphereCentre;
	//Iz jednadžbe: sqrLength(rayOrigin + rayDir * dst) = radius^2
	//Rješavanje kvadratne jednadžbe
	float a = dot(ray.direction, ray.direction); //smjer zrake mora biti normaliziran
	float b = 2 * dot(offsetRayOrigin, ray.direction);
	float c = dot(offsetRayOrigin, offsetRayOrigin) - sphereRadius * sphereRadius;
	//Računanje diskriminante
	float discriminant = b * b - 4 * a * c; 

	//ako je diskriminanta < 0, zraka nije pogodila sferu
	if (discriminant >= 0) {
	    //Udaljenost do najbliže točke presjeka
		float distance = (-b - sqrt(discriminant)) / (2 * a);

		//Udaljenost mora biti veća od 0, ignoriramo presjeke iza hvatišta zrake
		if (distance > 0) {
			//pripisivanje vrijednosti HitInfo
			hitInfo.didHit = true;
			hitInfo.distance = distance;
			hitInfo.hitPoint = ray.origin + ray.direction * distance;
			hitInfo.normal = normalize(hitInfo.hitPoint - sphereCentre);
			hitInfo.material = sphere.material;
			hitInfo.sphere.position = sphere.position;
			hitInfo.sphere.radius = sphere.radius;
			
		}
	}
	
    return hitInfo;
}

//Izračun kolizije zrake sa scenom
HitInfo CalculateRayCollision(Ray ray, HitInfo hitInfoPrevious) {
	HitInfo closestHit = (HitInfo)0;
	//Nismo još pogodili scenu pa je najbliži pogodak u beskonačnosti
	closestHit.distance = 1.#INF;

	uint brojSfera;
	uint stride;

	spheres.GetDimensions(brojSfera, stride);

	//Provjeri presjek sa svim sferama u sceni
	for (uint i = 0; i < brojSfera; i++)
	{
		Sphere trenutnaSfera = spheres[i];
		if (hitInfoPrevious.distance != 0) {
			//provjeri je li presjek vezan uz istu sferu, ako da ignoriraj presjek -> inače dovodi do ruznih titranja
			if (trenutnaSfera.position.x == hitInfoPrevious.sphere.position.x && trenutnaSfera.position.y == hitInfoPrevious.sphere.position.y && trenutnaSfera.position.z == hitInfoPrevious.sphere.position.z) {
				continue;
			}
		}
		
		HitInfo hitInfo = RaySphereIntersection(ray, trenutnaSfera);

		if (hitInfo.didHit && hitInfo.distance < closestHit.distance)
		{
			closestHit = hitInfo;
		}
	}

	return closestHit;
}

//Izracun reflektiranog vektora
float3 getReflectedVector(Ray ray, float3 normala) {
	float3 U = -ray.direction;
	float3 N = normala;
	float3 reflektiraniVektor = (dot(2 * U, N)) * N - U;
	return reflektiraniVektor;
}

//Jednostavan izračun sjene u točki
int Shading(HitInfo hitInfo) {

	//SHadow feeler
	Ray shaderRay;

	shaderRay.origin = hitInfo.hitPoint;

	shaderRay.direction = sceneLightPos - hitInfo.hitPoint;
	shaderRay.direction = normalize(shaderRay.direction);

	//ako zraka sjene ima presjek, vrati 1, inače vrati 0
	HitInfo shaderRayHitInfo = CalculateRayCollision(shaderRay, hitInfo);

	if (shaderRayHitInfo.didHit) {
		//ima sjenu
		return 1;
	} else {
		return 0;
	}

}

//Ambijentalna komponenta Phongovog osvjetljenja
float3 ambientLighting(HitInfo hitInfo) {
	//Boja materijala * intenzitet ambijentalnog svijetla
	float3 ambijentalniDoprinos = float3(
					hitInfo.material.ambientColor.x * ambientLight.x, 
					hitInfo.material.ambientColor.y * ambientLight.y, 
					hitInfo.material.ambientColor.z * ambientLight.z);

	
	return ambijentalniDoprinos;
}

//Difuzna komponenta
float3 diffuseLighting(HitInfo hitInfo) {

	//provjeri sjene
	if (shadows) {
		int shading = Shading(hitInfo);

		if (shading == 1) {
			return float3(0.0, 0.0, 0.0);
		}
	}
	
	//sve po formuli
	float3 L = sceneLightPos - hitInfo.hitPoint;
	L = normalize(L);

	float3 difuzniDoprinos = float3(
	sceneLightIntensity.x * hitInfo.material.diffuseColor.x * max(dot(L, hitInfo.normal), 0), 
	sceneLightIntensity.y * hitInfo.material.diffuseColor.y * max(dot(L, hitInfo.normal), 0), 
	sceneLightIntensity.z * hitInfo.material.diffuseColor.z * max(dot(L, hitInfo.normal), 0));

	return difuzniDoprinos;

}

//spekularna komponenta
float3 specularLighting(HitInfo hitInfo) {

	//provjeri sjenu
	if (shadows) {
		int shading = Shading(hitInfo);

		if (shading == 1) {
			return float3(0.0, 0.0, 0.0);
		}
	}

	//formula
	float3 L = sceneLightPos - hitInfo.hitPoint;
	L = normalize(L);

	float3 V = cameraPos - hitInfo.hitPoint;
	V = normalize(V);

	float3 U = L;
	U = normalize(U);
	float3 N = hitInfo.normal;
	N = normalize(N);
	float3 R = (dot(2 * U, N)) * N - U;
	R = normalize(R);

	float3 spekularniDoprinos = float3(
	sceneLightIntensity.x * hitInfo.material.specularColor.x * pow(max(dot(R, V), 0) , hitInfo.material.specularFactor), 
	sceneLightIntensity.y * hitInfo.material.specularColor.y * pow(max(dot(R, V), 0), hitInfo.material.specularFactor), 
	sceneLightIntensity.z * hitInfo.material.specularColor.z * pow(max(dot(R, V), 0), hitInfo.material.specularFactor));

	return spekularniDoprinos;

}

//Blinn-Phong, mijenja izračun spekularne komponente sa halfway vektorom
float3 blinnPhongSpecularLighting(HitInfo hitInfo) {

	//provjeri sjenu
	if (shadows) {
		int shading = Shading(hitInfo);

		if (shading == 1) {
			return float3(0.0, 0.0, 0.0);
		}
	}


	float3 L = sceneLightPos - hitInfo.hitPoint;
	L = normalize(L);

	float3 V = cameraPos - hitInfo.hitPoint;
	V = normalize(V);

	float3 halfwayDir = normalize(L + V);

	float3 N = hitInfo.normal;
	N = normalize(N);

	float3 spekularniDoprinos = float3(
	sceneLightIntensity.x * hitInfo.material.specularColor.x * pow(max(dot(N, halfwayDir), 0) , hitInfo.material.specularFactor), 
	sceneLightIntensity.y * hitInfo.material.specularColor.y * pow(max(dot(N, halfwayDir), 0), hitInfo.material.specularFactor), 
	sceneLightIntensity.z * hitInfo.material.specularColor.z * pow(max(dot(N, halfwayDir), 0), hitInfo.material.specularFactor));

	return spekularniDoprinos;
}

//Izračun sveukupnog Phongovog osvjetljenja
float3 PhongShading(Ray ray, HitInfo hitInfoPrevious) {

	float3 phongBoja = float3(0.0, 0.0, 0.0);
	pogodioPozadinu = 0;

	HitInfo hitInfo = CalculateRayCollision(ray, hitInfoPrevious);

	if (hitInfo.didHit) {

		if (blackWhite) {
			return float3(1.0, 1.0, 1.0);
		}

		//izracunaj phong osvijetljenje

		if (ambient) {
			float3 ambijentalniDoprinos = ambientLighting(hitInfo);
			phongBoja += ambijentalniDoprinos;
		}
		

		if (diffuse) {
			float3 difuzniDoprinos = diffuseLighting(hitInfo);
			phongBoja += difuzniDoprinos;
		}
		
		if (specular) {
			float3 spekularniDoprinos;
			if (blinnPhong) {
				spekularniDoprinos = blinnPhongSpecularLighting(hitInfo);
			} else {
				spekularniDoprinos = specularLighting(hitInfo);
			}
			
			phongBoja += spekularniDoprinos;
		}
		
	} else if (blackBackground || blackWhite) {
		//ako je crna pozadina flag uključen ili blackWhite vrati crnu boju
		return float3(0.0, 0.0, 0.0);
	} else {
		//Inače vrati boju pozadine iz Unity editora
		pogodioPozadinu = 1;
	}

	return phongBoja;

}

//izračun reflektirane zrake
Ray CalculateReflectedRay(Ray ray, HitInfo hitInfo, float reflectionCoef) {

	Ray reflectedRay = ray;
	float3 originRef = hitInfo.hitPoint;

	reflectedRay.origin = originRef;
	reflectedRay.energy = float3(1.0, 1.0, 1.0);

	float3 U = -ray.direction;
	U = normalize(U);

	float3 N = hitInfo.normal;
	N = normalize(N);

	float3 directionRef = (dot(2 * U, N)) * N - U;

	reflectedRay.direction = directionRef;
				
	reflectedRay.energy *= reflectionCoef;

	return reflectedRay;

}

//Izračun refraktirane zrake
Ray CalculateRefractedRay(Ray ray, HitInfo hitInfo, float refractionCoef) {
	Ray refractedRay = ray;
	float3 U = ray.origin - hitInfo.hitPoint;
	U = normalize(U);
	float alfa;
	float cosA;
	float a;
	float D;
	alfa = acos(dot(U, hitInfo.normal) / (length(U) * length(hitInfo.normal)));
	cosA = cos(alfa);
	cosA = (cosA * 180.0 / PI);

	D = 4 * (hitInfo.material.indeksLoma * hitInfo.material.indeksLoma * cosA * cosA - hitInfo.material.indeksLoma * hitInfo.material.indeksLoma + 1);

	a = (-2 * hitInfo.material.indeksLoma * cosA + sqrt(D)) / 2;

	float3 R = -a * hitInfo.normal - hitInfo.material.indeksLoma * U;
	R = normalize(R);

	refractedRay.origin = hitInfo.hitPoint;
	refractedRay.direction = R;
	refractedRay.energy *= refractionCoef;

	return refractedRay;

}


//funkcija za praćenje zrake
float3 TraceRay(Ray ray, int dubina, int type) {

	//type = 0 -> reflection
	//type = 1 -> refraction
	float3 tracedColor = float3(0.0, 0.0, 0.0);
	//Izračun presjeka zrake s scenom
	HitInfo hitInfo = CalculateRayCollision(ray, (HitInfo)0);
	//Prijašnji presjek sa scenom, treba nam za ispravni izračun presjeka sa scenom
	HitInfo hitInfoPrevious;

	

	if (type == 0 && hitInfo.didHit) {
		//za prvu kuglu koju je pogodio sacuvaj koeficijent, taj koeficijent će se uvijek davati funkciji za refleksiju
		float reflectionCoefMat = hitInfo.material.reflectionCoef;
		//izracunaj reflektiranu zraku od zadane u funkciji
		Ray reflectedRay = CalculateReflectedRay(ray, hitInfo, reflectionCoefMat);

		//sada posalji reflektiranu zraku u loop sa dubinom i traceaj

		for (int bounceIndex = 0; bounceIndex < dubina; bounceIndex++) {
			
			//sačuvaj prijašnji hiTInfo
			hitInfoPrevious = hitInfo;
	
			hitInfo = CalculateRayCollision(reflectedRay, hitInfoPrevious);

			if (hitInfo.didHit) {
				//izracunaj phong za tu tocku i idi dalje
				float3 phong = PhongShading(reflectedRay, hitInfoPrevious);

				//energija zrake se smanjuje za reflekcijski koeficijent svakim izračunom
				tracedColor += phong * reflectedRay.energy;

				//reflektiraj zraku opet

				reflectedRay = CalculateReflectedRay(reflectedRay, hitInfo, reflectionCoefMat);
			} else {
				return tracedColor;
			}
			
		}

		
	} else if (type == 1 && hitInfo.didHit) {
		//refrakcijska zraka
		float refractionCoefMat = hitInfo.material.refractionCoef;
		Ray refractedRay = CalculateRefractedRay(ray, hitInfo, refractionCoefMat);
			

		//sada posalji refraktiranu zraku u loop sa dubinom i traceaj

		for (int bounceIndex = 0; bounceIndex < dubina; bounceIndex++) {
			//Sačuvaj prijašnji hitInfo
			hitInfoPrevious = hitInfo;

			hitInfo = CalculateRayCollision(refractedRay, hitInfoPrevious);

			if (hitInfo.didHit && length(refractedRay.energy) > 0.1) {
				//izracunaj phong za tu tocku i idi dalje
				float3 phong = PhongShading(refractedRay, hitInfoPrevious);

				tracedColor += phong * refractedRay.energy;

				//refraktiraj zraku opet 

				refractedRay = CalculateRefractedRay(refractedRay, hitInfo, hitInfo.material.refractionCoef);
			} else {
				return tracedColor;
			}
		}
	}

	return tracedColor;



}

//Broj paralelnih grupa dretvi za program, koristi se jedna dretva po pikselu
[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    //na temelju piksela i objekata u sceni treba izracunati doprinose zraka

	float3 sveukupnaBoja = float3(0.0, 0.0, 0.0);

    //izracunaj zraku kroz piksela
	uint width, height;
    Result.GetDimensions(width, height);
	//Izračun pozicije piksela na ekranu
	float2 uv = float2((id.xy + float2(0.5f, 0.5f)) / float2(width, height) * 2.0f - 1.0f);

    Ray trenutniRay = CreateCameraRay(uv);

	//Doprinos Phong shadinga
	float3 phongColor = PhongShading(trenutniRay, (HitInfo)0);
	sveukupnaBoja += phongColor;

	//Ako je uključen dio za praćenje zrake, onda ga dodaj konačnom izračunu
	if (reflection) {
		
		float3 tracedReflectionColor = TraceRay(trenutniRay, bounceNumber, 0);
		sveukupnaBoja += tracedReflectionColor;

	}

	if (refraction) {

		float3 tracedRefractionColor = TraceRay(trenutniRay, bounceNumber, 1);
		sveukupnaBoja += tracedRefractionColor;

	}
	
	//Ako je zraka pogodila pozadinu onda vrati originalnu Unity pozadinu
	if (pogodioPozadinu == 1) {
		Result[id.xy] = float4(Result[id.xy].x, Result[id.xy].y, Result[id.xy].z, 0.0);
		
	} else{
		Result[id.xy] = float4(sveukupnaBoja.x, sveukupnaBoja.y, sveukupnaBoja.z, 0.0);
	}
}

